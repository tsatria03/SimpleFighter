class sonar
{
    bool enabled = true;
    bool follow_player = true;
    bool both_directions = false;
int sonarsound;
    int mode = 0;
    int loop_speed = 500;
int scan_range = 5;
    string facedir="right";
    timer looptimer;
    void toggle_follow()
    {
        follow_player = !follow_player;
        if (follow_player)
            speak("Sonar follows you.");
        else
            speak("Sonar does not follow you.");
    }
    void toggle_both()
    {
        both_directions = !both_directions;
        if (both_directions)
            speak("Sonar goes in both directions at once.");
        else
speak("Sonar goes in one direction only.");
    }
    void cycle_mode()
    {
        mode++;
        if (mode > 2) mode = 0;
        if (mode == 0) speak("Sonar off.");
        if (mode == 1) speak("Sonar step mode.");
        if (mode == 2) speak("Sonar loop mode.");
    }
    void cycle_speed()
    {
        if (loop_speed == 1000) loop_speed = 500;
        else if (loop_speed == 500) loop_speed = 250;
        else if (loop_speed == 250) loop_speed = 100;
        else loop_speed = 1000;
        if (loop_speed == 1000) speak("Slow.");
        if (loop_speed == 500) speak("Medium.");
        if (loop_speed == 250) speak("Fast.");
        if (loop_speed == 100) speak("Super fast.");
    }
    void face(string dir)
    {
        facedir = dir;
    }
    void sonarcheck()
    {
        if (!enabled || mode == 0)
            return;
        if (mode == 2)
        {
            if (looptimer.elapsed < loop_speed)
                return;
            looptimer.restart();
        }
        string dir;
        if (follow_player)
            dir = direction;
        else
            dir = facedir;
        if (both_directions)
        {
            scan_direction("left", 5);
            scan_direction("right", 10);
        }
        else
        {
            scan_direction(dir, 0);
        }
        if (jumping == 1)
        {
            scan_direction("down", 0);
        }
    }
void scan_direction(string dir, double pitch_offset)
{
    double dx = 0;
    double dy = 0;
    if (dir == "left") dx = -1;
    if (dir == "right") dx = 1;
    if (dir == "up") dy = -1;
    if (dir == "down") dy = 1;
    double tx = me.x;
    double ty = me.y;
    for (int i = 1; i <= scan_range; i++)
    {
        tx = me.x + dx * i;
        ty = me.y + dy * i;
        if (string_contains(gmt(tx, ty), "wall", 1) > -1)
        {
            play_sonar("wall", tx, ty, pitch_offset);
            return;
        }
        for (uint h = 0; h < hazards.length(); h++)
        {
            if (tx >= hazards[h].minx && tx <= hazards[h].maxx && ty >= hazards[h].miny && ty <= hazards[h].maxy)
            {
                play_sonar("hazard", tx, ty, pitch_offset);
                return;
            }
        }
        for (uint s = 0; s < staircases.length(); s++)
        {
            if (tx >= staircases[s].minx && tx <= staircases[s].maxx && ty >= staircases[s].miny && ty <= staircases[s].maxy)
            {
                if (ty < staircases[s].maxy)
                    play_sonar("up", tx, ty, pitch_offset);
                else
                    play_sonar("down", tx, ty, pitch_offset);
                return;
            }
        }
        for (uint v = 0; v < vanishing_platforms.length(); v++)
        {
            if (tx >= vanishing_platforms[v].minx && tx <= vanishing_platforms[v].maxx && ty >= vanishing_platforms[v].miny && ty <= vanishing_platforms[v].maxy)
            {
                play_sonar("vanish", tx, ty, pitch_offset);
                return;
            }
        }
        string tile = gmt(tx, ty);
        if (tile != "")
        {
            play_sonar("tile", tx, ty, pitch_offset);
            return;
        }
    }
    play_sonar("air", me.x + dx * scan_range, me.y + dy * scan_range, pitch_offset);
}
void play_sonar(string type, double sx, double sy, double pitch_offset)
{
    string sonarfile;
    if (type == "wall")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*wall*");
    else if (type == "hazard")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*hazard*");
    else if (type == "up")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*up*");
    else if (type == "down")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*down*");
    else if (type == "vanish")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*vanish*");
    else if (type == "tile")
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*tile*");
    else
        sonarfile = get_pack_sound("map/" + maptype + "/sonar/*air*");
    sonarsound = sonarpool.play_extended_2d(sonarfile, me.x, me.y, sx, sy, 0, 0, 0, 0, false, 0, 0, 0, 100+pitch_offset, false);
}
}
